## Perform analyses for revised manuscript (revision round 1)

configfile: "config/config.yml"

# ENCODE-rE2G prediction and enhancer list files
configfile: "config/encode_re2g_predictions.yml"
configfile: "config/encode_re2g_enhancer_lists.yml"

# include rules for all analyses
include: "rules/manuscript.smk"
include: "rules/enhancer_definitions.smk"

rule all:
  input:
    "results/manuscript/main_fig1b_prkar2b_locus.html",
    "results/manuscript/main_crispr_benchmarks.html",
    "results/manuscript/heldout_crispr_benchmarks.html",
    "results/manuscript/main_fig4e_gwas_locus.html",
    "results/manuscript/plots/main_fig5a_prkar2b_features.pdf",
    "results/manuscript/assaying_enhancer_activity_figures.html",
    "results/manuscript/main_fig5_additional_models.html",
    "results/manuscript/extended_data_fig1_feature_correlation.html",
    "results/manuscript/extended_data_fig2_crispr_benchmarking.html",
    "results/manuscript/extended_data_fig3a_perf_eg_classes.html",
    "results/manuscript/extended_data_fig3_additional_models.html",
    "results/manuscript/plots/extended_data_fig6i_activity_vs_distance.pdf",
    "results/manuscript/plots/extended_data_fig7_enhancer_definitions.pdf",
    "results/manuscript/crispr_supplementary_note_S1.1.html",
    "results/manuscript/indirect_effects_analyses.html",
    "results/manuscript/figS7_additional_crispr_benchmarks.html",
    "results/manuscript/figS8_predictors_vs_crispr_effects.html",
    "results/manuscript/plots/figS9_crispr_locus_plots/hbe1_locus_plot.pdf",
    "results/manuscript/plots/figS9_crispr_locus_plots/myc_locus_plot.pdf",
    "results/manuscript/plots/figS9_crispr_locus_plots/myc_locus_plot_zoomed.pdf",
    "results/manuscript/plots/figS9_crispr_locus_plots/gata1_locus_plot.pdf",
    "results/manuscript/plots/figS11_encode_re2g_vs_gene_expression.pdf",
    "results/manuscript/tables/table_s2_metadata_encode_re2g_predictions.csv",
    "results/manuscript/tables/table_s2_metadata_encode_re2g_ext_predictions.csv",
    "results/manuscript/tables/table_s2_metadata_encode_re2g_predictions_combined.csv",
    "results/manuscript/tables/table_s9_baseline_predictors.csv",
    "results/manuscript/tables/table_s10_performance_summary.csv",
    "results/manuscript/tables/table_s15_enhancer_activity_files.csv",
    "results/manuscript/tables/table_s16_encode_re2g_stats_per_experiment.csv",
    "results/manuscript/tables/table_s17_encode_re2g_stats_per_gene.csv"

rule download_genome_annotation:
  output: "resources/gencode.v29.annotation.gtf.gz"
  params:
    url = "https://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_human/release_29/gencode.v29.annotation.gtf.gz"
  conda: "envs/analyses_env.yml"
  shell:
    "wget -O {output} {params.url}"
    
rule download_encode_file:
  output: "resources/{accession}.bed.gz"
  params:
    url = "https://www.encodeproject.org/files/{accession}/@@download/{accession}.bed.gz"
  conda: "envs/analyses_env.yml"
  shell:
    "wget -O {output} {params.url}"
    
rule download_encode_bigwig:
  output: "resources/{accession}.bigWig"
  params:
    url = "https://www.encodeproject.org/files/{accession}/@@download/{accession}.bigWig"
  conda: "envs/analyses_env.yml"
  shell:
    "wget -O {output} {params.url}"  
    
rule download_encode_bam:
  output:
    bam = temp(config["scratch_dir"] + "/{accession}.bam"),
    sorted_bam = config["scratch_dir"] + "/{accession}.sorted.bam",
    sorted_bai = config["scratch_dir"] + "/{accession}.sorted.bam.bai"
  params:
    url = "https://www.encodeproject.org/files/{accession}/@@download/{accession}.bam"
  conda: "envs/analyses_env.yml"
  resources:
    mem = "32G"
  shell:
    "wget -O {output.bam} {params.url}; "
    "samtools sort -o {output.sorted_bam} {output.bam}; "
    "samtools index {output.sorted_bam}"
